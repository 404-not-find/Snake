apply plugin: 'com.android.application'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.androidTools

    defaultConfig {
        applicationId "com.youngfeng.snake.demo"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk
        versionCode 110
        versionName "1.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    dataBinding {
        enabled = true
        enabledForTests = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        Properties properties = new Properties()
        InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
        properties.load(inputStream)

        def alias = properties.getProperty("sign.alias")
        def keyPasswd = properties.getProperty("sign.keyPasswd")
        def storePasswd = properties.getProperty("sign.storePasswd")

        release {
            keyAlias alias
            keyPassword keyPasswd
            storePassword storePasswd
            storeFile file('keystore/keystore.jks')
        }
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api deps.butterknife
    annotationProcessor deps.butterknifeCompiler

    implementation project(":snake")
    annotationProcessor project(":snake-compiler")

    implementation deps.androidx.compat

    implementation(deps.autoScrollViewPager) {
        exclude group: 'com.android.support' , module: 'recyclerview-v7'
    }

    implementation deps.crashreportUpgrade
    implementation deps.photoView
    implementation deps.circleImageView
    implementation deps.easyPermission
    implementation deps.jetpack.lifecycle
    annotationProcessor deps.jetpack.lifecycleCompiler
    implementation deps.jetpack.lifecycleCompilerJ8
}
